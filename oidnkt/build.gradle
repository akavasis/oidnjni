buildscript {
    apply from: rootProject.file('versions.gradle')
}

plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
}

group 'net.time4tea'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    project(":oidnjni")
    implementation kotlin
    testImplementation testing
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

/* generate headers to look at, not used for code gen */
task javah(type: Exec) {
    def classpath = sourceSets.main.output.classesDirs.join(File.pathSeparator)
    println(classpath)
    commandLine "javah", "-classpath", classpath, "-d", "$buildDir/generated/jni", "net.time4tea.oidn.Oidn", "net.time4tea.oidn.OidnDevice", "net.time4tea.oidn.OidnFilter"
    dependsOn classes
}

test {
    outputs.upToDateWhen {false}
    useJUnitPlatform()
    systemProperty "java.library.path", file("${project(":oidnjni").buildDir}").absolutePath
    environment "LD_LIBRARY_PATH", file("${project(":oidnjni").buildDir}/oidn-1.1.0.x86_64.linux/lib").absolutePath
}

build.dependsOn  ':oidnjni:make'
clean.dependsOn  ':oidnjni:makeClean'